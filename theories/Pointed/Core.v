(* -*- mode: coq; mode: visual-line -*- *)
Require Import Basics.
Require Import Types.

Declare Scope pointed_scope.

Local Open Scope pointed_scope.
Local Open Scope path_scope.

Generalizable Variables A B f.

(** * Pointed Types *)

(** A sigma type of pointed components is pointed. *)
Global Instance ispointed_sigma `{IsPointed A} `{IsPointed (B (point A))}
: IsPointed (sigT B)
  := (point A; point (B (point A))).

(** A cartesian product of pointed types is pointed. *)
Global Instance ispointed_prod `{IsPointed A, IsPointed B} : IsPointed (A * B)
  := (point A, point B).

(* Product of pTypes is a pType *)
Notation "X * Y" := (Build_pType (X * Y) ispointed_prod) : pointed_scope.

(** ** Pointed functions *)

(* A pointed map is a map with a proof that it preserves the point *)
Record pMap (A B : pType) :=
  { pointed_fun : A -> B ;
    point_eq : pointed_fun (point A) = point B }.

Arguments point_eq {A B} f : rename.
Arguments pointed_fun {A B} f : rename.
Coercion pointed_fun : pMap >-> Funclass.

Infix "->*" := pMap : pointed_scope.

Definition pmap_idmap {A : pType} : A ->* A
  := Build_pMap A A idmap 1.

Definition pmap_compose {A B C : pType}
           (g : B ->* C) (f : A ->* B)
: A ->* C
  := Build_pMap A C (g o f)
                (ap g (point_eq f) @ point_eq g).

Infix "o*" := pmap_compose : pointed_scope.

(** ** Pointed homotopies *)

(* A pointed homotopy is a homotopy with a proof that the presevation
   paths agree *)
Record pHomotopy {A B : pType} (f g : pMap A B) :=
  { pointed_htpy : f == g ;
    point_htpy : pointed_htpy (point A) @ point_eq g = point_eq f }.

Arguments Build_pHomotopy {A B f g} p q : rename.
Arguments point_htpy {A B f g} p : rename.
Arguments pointed_htpy {A B f g} p x.

Coercion pointed_htpy : pHomotopy >-> pointwise_paths.

Infix "==*" := pHomotopy : pointed_scope.

(** ** Pointed equivalences *)

(* A pointed equivalence is a pointed map and a proof that it is
  an equivalence *)
Record pEquiv (A B : pType) :=
  { pointed_equiv_fun : A ->* B ;
    pointed_isequiv : IsEquiv pointed_equiv_fun
  }.

(* TODO:
  It might be better behaved to define pEquiv as an equivalence and a proof that
  this equivalence is pointed.
    In pEquiv.v we have another constructor Build_pEquiv' which coq can
  infer faster than Build_pEquiv.
  *)

Infix "<~>*" := pEquiv : pointed_scope.

Coercion pointed_equiv_fun : pEquiv >-> pMap.
Global Existing Instance pointed_isequiv.

Coercion pointed_equiv_equiv {A B} (f : A <~>* B)
  : A <~> B := Build_Equiv A B f _.

(* Pointed type family *)
Definition pFam (A : pType) := {P : A -> Type & P (point A)}.
Definition pfam_pr1 {A : pType} (P : pFam A) : A -> Type := pr1 P.
Coercion pfam_pr1 : pFam >-> Funclass.

(* IsTrunc for a pointed type family *)
Class IsTrunc_pFam n {A} (X : pFam A)
  := trunc_pfam_is_trunc : forall x, IsTrunc n (X.1 x).

(* Pointed sigma *)
Definition psigma {A : pType} (P : pFam A) : pType.
Proof.
  exists {x : A & P x}.
  exact (point A; P.2).
Defined.

(* Pointed pi types, note that the domain is not pointed *)
Definition pforall {A : Type} (F : A -> pType) : pType
  := Build_pType (forall (a : A), pointed_type (F a)) (ispointed_type o F).

(** The following tactics often allow us to "pretend" that pointed maps and homotopies preserve basepoints strictly.  We have carefully defined [pMap] and [pHomotopy] so that when destructed, their second components are paths with right endpoints free, to which we can apply Paulin-Morhing path-induction. *)

(** First a version with no rewrites, which leaves some cleanup to be done but which can be used in transparent proofs. *)
Ltac pointed_reduce' :=
  unfold pointed_fun, pointed_htpy; cbn;
  repeat match goal with
           | [ X : pType |- _ ] => destruct X as [X ?point]
           | [ phi : pMap ?X ?Y |- _ ] => destruct phi as [phi ?]
           | [ alpha : pHomotopy ?f ?g |- _ ] => destruct alpha as [alpha ?]
           | [ equiv : pEquiv ?X ?Y |- _ ] => destruct equiv as [equiv ?iseq]
         end;
  cbn in *; unfold point in *;
  path_induction; cbn.

(** Next a version that uses [rewrite], and should only be used in opaque proofs. *)
Ltac pointed_reduce :=
  pointed_reduce';
  rewrite ?concat_p1, ?concat_1p.

(** Finally, a version that just strictifies a single map or equivalence.  This has the advantage that it leaves the context more readable. *)
Ltac pointed_reduce_pmap f
  := try match type of f with
    | pEquiv ?X ?Y => destruct f as [f ?iseq]
    end;
    match type of f with
    | _ ->* ?Y => destruct Y as [Y ?], f as [f p]; cbn in *; destruct p; cbn
    end.


(** ** Equivalences *)

Definition issig_ptype : { X : Type & X } <~> pType.
Proof.
  issig.
Defined.

Definition issig_pmap (A B : pType)
: { f : A -> B & f (point A) = point B } <~> (A ->* B).
Proof.
  issig.
Defined.
